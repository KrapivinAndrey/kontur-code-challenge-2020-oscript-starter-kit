#Использовать asserts
#Использовать json

// чтение данных на ввод
Перем Консоль;

// точка куда летим
Перем ЦентрГалактики;

// разбор JSON
Перем ПарсерJSON;

Процедура ЦиклЖизни()
	
// Расстановка сил

	Драфт = ПолучитьСостояние();
	ОтправитьОтвет(ПустойОтвет());

// Битва

	Пока Истина Цикл
		
		ТекСостояние = ПолучитьСостояние();
		Если ТекСостояние = Неопределено Тогда

			Продолжить;

		КонецЕсли;

		Ответ = ПодготовитьОтвет(ТекСостояние);

		ОтправитьОтвет(Ответ);

	КонецЦикла;

КонецПроцедуры

#Область Ввод_Вывод

Функция ПолучитьСостояние()

	ВхСтрока  = Консоль.ПрочитатьСтроку();
	Если ВхСтрока = Неопределено 
			ИЛИ ВхСтрока = "" Тогда

		Возврат Неопределено;

	КонецЕсли;

	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ВхСтрока);
	Результат = ПрочитатьJSON(ЧтениеJSON, Истина);

	Возврат Результат;
	
КонецФункции

Процедура ОтправитьОтвет(Ответ)

    ВалидныйОтветСтрока = ПарсерJSON.записатьJSON(Ответ);
    ВалидныйОтветСтрока = СтрЗаменить(ВалидныйОтветСтрока, Символы.ПС, "");
	ВалидныйОтветСтрока = СтрЗаменить(ВалидныйОтветСтрока, " ", "");
	
	Консоль.ВывестиСтроку(ВалидныйОтветСтрока);
	
КонецПроцедуры

#КонецОбласти

Функция ПодготовитьОтвет(СостояниеМира)

	Ответ = Новый Соответствие();

	Для Каждого Корабль Из СостояниеМира["My"] Цикл

		ДобавитьКоманду(Ответ, КомандаДвигаться(Корабль, ЦентрГалактики));
		
	КонецЦикла;

	ДобавитьСообщение(Ответ, "Rulezzz");

	Возврат Ответ;

КонецФункции


Процедура ДобавитьКоманду(СоотОтвет, Команда)
	
	Если СоотОтвет["UserCommands"] = Неопределено Тогда
		СоотОтвет.Вставить("UserCommands", Новый Массив());
	КонецЕсли;

	СоотОтвет["UserCommands"].Добавить(Команда);

КонецПроцедуры

Процедура ДобавитьСообщение(Ответ, Сообщение)

	Ответ.Вставить("Message", Сообщение);

КонецПроцедуры

#Область Команды_кораблям

Функция КомандаДвигаться(Корабль, Точка)

	Результат = Новый Структура();
	Результат.Вставить("Command", "MOVE");
	Результат.Вставить("Parameters", НовыйПараметрыДвижения(Корабль, Точка));

	Возврат Результат;

КонецФункции

Функция КомандаСтрелять(Корабль, Цель, Орудие = Неопределено)

	Если Орудие = Неопределено Тогда

		Орудие = ОсновноеОрудиеКорабля(Корабль);
	
	КонецЕсли;

	Результат = Новый Структура();
	Результат.Вставить("Command", "ATTACK");
	Результат.Вставить("Parameters", НовыйПараметрыВыстрел(Корабль, Орудие, Цель));

	Возврат Результат;

КонецФункции

Функция НовыйПараметрыДвижения(Корабль, Цель)

	Возврат Новый Структура("Id, Target",
								Корабль["Id"],
								ВекторСтрокой(Цель));
КонецФункции

Функция НовыйПараметрыВыстрел(Корабль, Орудие, Цель)

	Возврат Новый Структура("Id, Name, Target",
								Корабль["Id"],
								Орудие,
								ВекторСтрокой(Цель));

КонецФункции

#КонецОбласти

Функция ПустойОтвет()

	Возврат Новый Структура;

КонецФункции

Функция ОсновноеОрудиеКорабля(Корабль)
	
	Оборудование = Корабль["Equipment"];
	
	Для Каждого Элемент Из Оборудование Цикл
		
		Если Элемент["Type"] = 1 Тогда
			Возврат Элемент;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;	
	
КонецФункции

Функция НовыйВектор(x, y = Неопределено, z = Неопределено)

	Если ТипЗнч(x) = Тип("Строка") Тогда

		ЭлементыВектора = СтрРазделить(x, "/");
		Возврат Новый Структура("x, y, z",
									Число(ЭлементыВектора[0]),
									Число(ЭлементыВектора[1]),
									Число(ЭлементыВектора[2])
								);

	Иначе

		Возврат Новый Структура("x, y, z",
									x,
									y,
									z
								);

	КонецЕсли;

КонецФункции

Функция ВекторСтрокой(Вектор)

	Возврат СтрШаблон("%1/%2/%3", Вектор.x, Вектор.y, Вектор.z); 

КонецФункции

Консоль 		= Новый Консоль();
ПарсерJSON 		= Новый ПарсерJSON();
ЦентрГалактики	= НовыйВектор(15,15,15);

ЦиклЖизни();
